apply plugin: 'com.android.application'
apply from: "../common/constants.gradle"
apply from: "../common/release.gradle"

android {
    compileSdkVersion project.sdk
    buildToolsVersion project.buildTools

    defaultConfig {
        applicationId "com.tneciv.zhihudaily"
        minSdkVersion project.minSdk
        targetSdkVersion project.sdk
        versionCode createDate()
        versionName "3.4.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        jackOptions {
            enabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
        }

        // edit output apk
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def apkFile = new File(
                        output.outputFile.getParent(),
                        "ZhihuDaily-v${variant.versionName}-${variant.versionCode}-${variant.flavorName}.apk")
                output.outputFile = apkFile
            }
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
        prod {
        }

        dev {
            applicationId 'com.tneciv.zhihudaily.dev'
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile "com.android.support:appcompat-v7:${project.ext.supportLibraryVersion}"
    compile "com.android.support:design:${project.ext.supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${project.ext.supportLibraryVersion}"
    compile "com.android.support:support-v4:${project.ext.supportLibraryVersion}"
    compile "com.android.support:cardview-v7:${project.ext.supportLibraryVersion}"

    compile "com.jakewharton:butterknife:${project.ext.butterknifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${project.ext.butterknifeVersion}"

    compile 'com.tencent.bugly:crashreport:latest.release'

    debugCompile "com.squareup.leakcanary:leakcanary-android:${project.ext.leakcanaryVersion}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${project.ext.leakcanaryVersion}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${project.ext.leakcanaryVersion}"

    compile "com.squareup.okhttp3:logging-interceptor:${project.ext.okhttpVersion}"
    compile "com.squareup.retrofit2:retrofit:${project.ext.retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${project.ext.retrofitVersion}"
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    compile 'io.reactivex.rxjava2:rxandroid:2.0.0'
    compile 'io.reactivex.rxjava2:rxjava:2.0.0'

    testCompile 'junit:junit:4.12'
}
